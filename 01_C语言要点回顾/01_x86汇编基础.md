# x86汇编基础

> x86汇编存在intel和AT&T两种写法。一般windows用intel写法，unix用at&t写法。这里是后者

## 最简单的汇编程序

一般来讲，汇编程序的文件以`.s`结尾。用汇编器汇编之后的目标文件以`.o`结尾。

GNU中有两个工具:

- as，汇编器（assembler），把`.s`的汇编程序翻译成机器指令，生成目标文件
- ld，linker，把`.o`的目标文件链接成可执行程序

现在来看一个最简单的汇编程序，相当于在c的main函数中一个`return 4`。`echo $?`可以打印上次执行的命令的退出状态

```
 .section .data

 .section .text
 .globl _start
_start:
 movl $1, %eax    # this is linux kernel command number (system call) 
                  # for exiting a program
 movl $4, %ebx

 int $0x80        # this wakes up kernel to run the exit command
```

先汇编，再链接，再执行，然后看看退出状态是4。这就是我们预期的结果

```
(base) root test (master) # as -o hello.o hello.s 
(base) root test (master) # ld -o hello hello.o
(base) root test (master) # ./hello 
(base) root test (master) # echo $?
4
```

现在来分析一下这段汇编程序。

汇编程序中以`.`开头的不是助记符，不会被翻译成机器指令。这时给汇编器看的`汇编指示（Assembler Directive）`

- `.section`的作用是把指令划分为若干段。程序加载时，操作系统会把不同的段加载到不同的内存地址。
- `.data`段保存数据，可读可写。本程序没有数据，所以是空的
- `.text`段是代码段，可读可执行。后面的指令都属于`.text`段
- `_start`是一个符号，代表一个内存地址。c中执行一个函数，就是跳到函数代码的地址去执行。这里`_start`的作用类似于c中的`mian`。在`_start:`中的内容就会被最先执行。
- `.globl`把`_start`符号标记为一个全局符号。

```
 movl $1, %eax
```

这条语句的作用是在CPU中生成一个立即数（即在CPU内部产生的数）1，并送到寄存器eax中。

- $开头的数是立即数
- %开头后面接的是寄存器名
- mov表示是传送数据。l表示是long，即32位

```
 int $0x80
```

- int是软终端指令，可以产生一个异常。CPU会从用户模式切换到特权模式，然后跳转到内核代码中执行异常处理程序。Linux的各种系统调用都是由`int $0x80`指令引发。eax的值是系统调用号，这里是1，即会调用`_exit`，ebx的值是传给_exit的参数，表示退出状态。

## x86的寄存器

通用寄存器有`eax`、`ebx`、`ecx`、`edx`、`edi`、`esi`。大多数都是通用的，但一些指令会对操作数存放的寄存器有限制。

特殊寄存器有：

- eip：程序计数器
- eflags：保存计算过程中的标志位，如进位标志、溢出标志、零标志、负数标志灯
- ebp，esp：用于维护函数调用的栈帧

## 第二个汇编程序 -- 求一个数组的最大值

这个程序找出数组中的最大数，然后作为程序的退出状态。

寄存器的使用：

- eax，保存当前遍历到的数
- ebx，保存最大值
- edi，保存数组的索引

```
 .section .data
data_items:
 .long 3,67,34,222,45,75,54,34,44,33,22,11,66,0

 .section .text
 .globl _start
_start:
 movl $0, %edi                  # move 0 to the index register
 movl data_items(,%edi,4), %eax # 把edi索引的元素移动到eax，4表示元素占4字节
 movl %eax, %ebx                # 然后把eax的值初始化为最大值存到ebx中

start_loop:

 # cmpl会相减两个操作数，是0则退出
 # je可以理解为jump if equal，和cpml配合使用。cmpl改变会标志位。je根据标志位判断
 cmpl $0, %eax
 je loop_exit

 incl %edi                      # load next value
 movl data_items(,%edi,4), %eax

 # 比较当前数和最大值，jle是“jump if less than or equal”
 cmpl %ebx, %eax
 jle start_loop

 movl %eax, %ebx                # move the value as the largest
 jmp start_loop                 # jmp是无条件jump

loop_exit:
 movl $1, %eax                  # call _exit, ebx is parameter, and store max value
 int $0x80
```

汇编、链接、运行

```
(base) root test (master) # as -o max.o max.s 
(base) root test (master) # ld -o max max.o
(base) root test (master) # ./max 
(base) root test (master) # echo $?
222
```

`.long`声明了一个数组，每个数占32位。`data_items`是符号，是数组的首地址。它没有用`.globl`声明，因为只在汇编程序内部使用。链接器用不到。

除了`.long`，常用数据声明还有

- `.byte`，每个数占8位
- `.ascii`，如`.ascii "Hello World"`，声明11个数，值为对应的ascii吗。注意这里结尾不会自动加`\0`

## 寻址方式

内存寻址通常可以表示为：

```
address_or_offset(%base_or_offset,%index,multiplier)
```

其中`address_or_offset`和`multiplier`必须是常数，`base_or_offset`和`index`必须是寄存器。可以省略，表示是0。

计算方法为：

```
final_address = address_or_offset + base_or_offset + index * multiplier
```

寻址方式：

- 直接寻址：只用address_or_offset，如`movl address, %eax`把address地址处的32位数传送到eax寄存器。
- 变址寻址（indexed addressing）：前面的`movl data_items(,%edi,4), %eax`就是这个。方便访问数组。
- 间接寻址（indirect addressing）：只用`base_or_offset`。如`movl (%eax), %ebx`，把eax寄存器的值看作地址，把内存中这个地址处的32位数传送到ebx寄存器。注意和`movl %eax, %ebx`区分开。
- 基址寻址（Base Pointer Addressing）：只用`address_or_offset`和`base_or_offset`，如`movl 4(%eax), %ebx`会把`eax值+4`作为地址，取元素后放到ebx中
- 立即数寻址：如`movl $12, %eax`，直接把12放到eax中。这其实没有寻址。
- 寄存器寻址：如`movl $12, %eax`，第二部分就是寄存器寻址。
